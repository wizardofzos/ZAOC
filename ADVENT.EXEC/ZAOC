/* REXX */
Address ISPExec

  'vget (zapplid)'
  /*
  "Control Errors Return"
  */
  parse value '' with null
  /* ----------------------------------------------------- *
  | Initialise the application named THISAPP              |
  * ----------------------------------------------------- */
  parse source x1 x2 x3 x4 exec .
  if exec = '?' then do
    x = listdsi(x4 'FILE')
    exec = sysdsname
  end

  hlq = translate(exec,' ','.')
  hlq = subword(hlq,1,words(hlq)-1)
  hlq = translate(hlq,'.',' ')

  exec = "'"exec"'"
  panels = "'"hlq".PANELS'"
  config = "'"hlq".CONFIG'"
  solutions = "'"hlq".SOLVE.REXX" /* No closing ' so we can add (*/

  if zapplid /= 'ZAOC' then do
    if panels /= null then
    'libdef ispplib dataset id('panels') stack'
    if exec /= null then
    Address TSO 'altlib act application(exec) dataset('exec')'
  end

  if zapplid <> 'ZAOC' then do
    "Select CMD(%zaoc) Newappl(ZAOC) Passlib"
    if panels /= null then
    'libdef ispplib'
    if exec /= null then
    Address TSO 'altlib deact application(exec)'
    exit 0
  end

  /* read config */
  curlpath = ''
  session = ''
  inputfiles = ''
  call read_config
  /* Now we start our main panel. */



  'TBOpen zaocmain Library(isptabl) Write Share'
  if rc > 0 then do
    if rc = 8 then do
      /* Create table, always add current year, day01*/
      "TBCreate zaocmain",
        "Names(year day part start tries finish)",
        "Library(isptabl) Write Share"
      "TBAdd zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
  end
  zcmd = null

  year_top = 0
  'tbsort zaocmain fields(year,n,d,day,n,d,part,n,a)'
  'tbtop zaocmain'
  do forever
    parse value '' with zsel zcmd row
    'TBTop zaocmain'
    'tbskip zaocmain number('year_top')'
    'tbdispl zaocmain panel(zaocmain)'

    if rc > 4 then leave

    repo_top = ztdtop
    if row = 0 then zsel = null
    if row <> null then
    if row > 0 then do
      'TBTop zaocmain'
      'TBSkip zaocmain Number('row')'
    end
    Select
      When abbrev('ADD',zcmd,2) = 1 then call Add_puzzle
      When abbrev('SOR',zcmd,2) = 1 then do
        'tbsort zaocmain fields(year,n,d,day,d,a,part,n,a)'
      end
      Otherwise nop
    End
    ys = substr(year,3,2)
    rx = solutions"(Y"ys"D"day"P"part")'"
    if zsel = 'E' then do
      if start = '' then do
        start = DATE("E") TIME()
        "TBPUT zaocmain"
        "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
      end
      'Edit dataset('rx')'
    end
    if zsel = 'R' then do
      Address TSO
      "EX "rx
      Address ISPEXEC
      tries = tries + 1
      "TBPUT zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
    if zsel = 'S' then do
      finish = DATE("E") TIME()
      "TBPUT zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
    if zsel = 'D' then do
      status = 'Tried'
      "TBDELETE zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
    if zsel = 'C' then do
      say "todo, read cobol, compile, run?"
    end
  end

  'TBCLOSE zaocmain'

  Exit 0

Add_puzzle:
  T = '1'
  'Addpop row(4) column(12)'
  'Display Panel(addpuzz)'
  /* PF3 = no add */
  if rc > 4 then do
    'Rempop'
    return
  end
  else do
    'Rempop'
  end
  /* Now we need to go out to adventofcode.com
  and get the puzzle for this day.. and the
  input file....                       */
  cmd = curlpath
  y = yyyy
  d = dd
  cmd = cmd "'https://adventofcode.com/"y"/day/"d"/input'"
  cmd = cmd "-H 'Accept: text/html'"
  cmd = cmd "-H 'Cookie: session="session";' -k > "inputfiles"/y"y"d"d
  x = bpxwunix(cmd,,so.,se.)
  if x = 0 then do
    year = yyyy
    day  = dd
    start  = ''
    tries  = 0
    finish = ''
    part   = 1
    'tbadd zaocmain'
    "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"

    part   = 2
    'tbadd zaocmain'
    "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
  end
  else do
    say "Retrieval of puzzle input for" yyyy "day" dd "failed"
    do i = 1 to so.0
      say so.i
    end
    do i = 1 to se.0
      say se.i
    end
  end
  /* create template solution */
  Address TSO
  ddname1 = "ZAOC#RX1"
  ddname2 = "ZAOC#RX2"
  ys = substr(y,3,2)
  plink = 'https://adventofcode.com/'yyyy'/day/'dd
  extras = 40 - length(plink)
  filler = ''
  do f = 1 to extras
    filler = filler || ' '
  end
  s.1 = "/*--------------------------------------------[REXX]"
  s.2 = "| Made with ZAOC by wizard@zdevops.com             |"
  s.3 = "| Puzzle: "plink filler || "|"
  s.4 = "+-------------------------------------------------*/"
  s.5 = 'say "Advent of Code' year 'day' day'"'
  s.6 = "x = bpxwunix('cat "inputfiles"/y"y"d"d"',,file.,se.)"
  s.7 = "do i = 1 to file.0"
  s.8 = "  say file.i"
  s.9 = "end"
  s.0 = 9
  "Alloc f("ddname1") shr reuse ds("solutions"(Y"ys"d"d"p1)')"
  'Execio * diskw' ddname1 '(finis stem s.'
  'Free  f('ddname1')'
  "Alloc f("ddname2") shr reuse ds("solutions"(Y"ys"d"d"p2)')"
  'Execio * diskw' ddname2 '(finis stem s.'
  'Free  f('ddname2')'
  Address ISPExec
  return

read_config: procedure,
 expose config curlpath session inputfiles
 parse var config
 Address TSO
 "Alloc f("ddname") shr reuse ds("config")"
 'Execio * diskr' ddname '(finis stem cfg.'
 'Free  f('ddname')'
 Address ISPExec
 do i = 1 to cfg.0
   if left(cfg.i,1) = '*' then iterate
   Select
     When translate(word(cfg.i,1)) = 'CURL_PATH' then
          curlpath  = word(cfg.i,3)
     When translate(word(cfg.i,1)) = 'SESSION' then
          session  = word(cfg.i,3)
     When translate(word(cfg.i,1)) = 'INPUTFILES' then do
            inputfiles  = word(cfg.i,3)
            x = bpxwunix('mkdir -p' inputfiles,,so.,se.)
          end
     Otherwise nop
   End
 end
 return


/* To add for Completion Post/Check

curl 'https://adventofcode.com/2015/day/1/answer'
     -X POST
     -H 'Accept: text/html
     -H 'Content-Type: application/x-www-form-urlencoded'
     -H 'Cookie: session=555...
     --data-raw 'level=1&answer=<answer>'

*/
